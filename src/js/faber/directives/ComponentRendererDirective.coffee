angular.module('faber').directive 'faberComponentRenderer', ($rootScope, $compile, componentsService)->
  restrict: 'AE'
  template: '<div ng-class="component.id"></div>'
  scope:
    isExpanded: '='
    block: '=faberComponentRendererBlock'
    checkGroupPreview: '&faberGroupPreview'

  link: ($scope, $element, $attrs)->
    $scope.isGroupPreview = $scope.checkGroupPreview() or false
    $scope.isSelected = false

    setComponent = (content)->
      template = $scope.component.template
      $component = $compile(template)($scope)

      wrapper = $element.find('div')
      wrapper.empty()
      $element.find('div').append $component

      if $scope.component.init
        return $scope.component.init($scope, $element, content) if $scope.component.type is 'group'

        $scope.component.init($scope, $element, content, $scope.updateRendered)

    $scope.component = null

    $scope.selectRendered = ()->
      $scope.isSelected = true
      $scope.component.selected($scope, $element, $scope.updateRendered) if $scope.component.selected

    $scope.unselectRendered = ()->
      $scope.isSelected = false
      $scope.component.unselected($scope, $element, $scope.updateRendered) if $scope.component.unselected

    $scope.updateRendered = (content)->
      if content
        $scope.block.content = content
        $rootScope.$broadcast 'BlockUpdated'

    $scope.$watch 'block.component', (val)->
      if !$scope.block
        return

      # retrieve the component's template and append it to the block
      $scope.component = componentsService.findById($scope.block.component)
      if $scope.block.component and $scope.component
        # group component initial content : angular.copy used to get rid of $$hashKey generated by angular for ng-repeat
        initialContent = if $scope.component.type is 'element' then $scope.block.content else angular.copy($scope.block.blocks)
        setComponent initialContent

    $scope.$on 'SelectBlockOfIndex', (evt, scope, index)->
      if !scope
        if $scope.isSelected
          $scope.unselectRendered()
        return

      if scope.block.blocks[index] is $scope.block
        $scope.selectRendered()
      else if $scope.isSelected
        $scope.unselectRendered()
